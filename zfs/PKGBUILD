_zfs_version="0.7.5"
_kernel_version="3.14.79-28" # set kernel version in zfs.install as well!

pkgname="zfs"
pkgver=${_zfs_version}_${_kernel_version/-/_}
pkgrel=1
license=('GPL')
pkgdesc="Solaris Porting Layer kernel modules and utils."
depends=("spl" "linux-odroid-c2=${_kernel_version}")
makedepends=("git" "linux-headers=${_kernel_version}")
arch=("aarch64")
url="http://zfsonlinux.org/"
source=(
    "git+https://github.com/zfsonlinux/zfs.git#tag=zfs-${_zfs_version}"
    zfs.conf
)
md5sums=(
    'SKIP'
    'SKIP'
)
install=zfs.install

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh

    unset LDFLAGS
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --sbindir=/usr/bin \
                --with-mounthelperdir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/lib/udev \
                --libexecdir=/usr/lib/zfs \
                --with-linux=/usr/lib/modules/${_kernel_version}-ARCH/build

    make
}

package() {
    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install

    cp -r "$pkgdir"/{lib,usr}
    rm -r "$pkgdir"/lib

    sed -i "s+${srcdir}++" ${pkgdir}/usr/src/zfs-*/${_kernel_version}-ARCH/Module.symvers

    install -D -t ${pkgdir}/etc/modprobe.d ../zfs.conf
}
