_zfs_version="0.7.6"
_kernel_version="3.14.79-28"

pkgname="zfs-odroid-c2"
pkgver=${_zfs_version}
pkgrel=2
license=('GPL')
pkgdesc="ZFS kernel modules and utils."
depends=("linux-odroid-c2=${_kernel_version}")
makedepends=("git" "linux-odroid-c2-headers=${_kernel_version}")
arch=("aarch64")
url="http://zfsonlinux.org/"
source=(
    "git+https://github.com/zfsonlinux/spl.git#tag=spl-${_zfs_version}"
    "git+https://github.com/zfsonlinux/zfs.git#tag=zfs-${_zfs_version}"
    zfs.conf
)
md5sums=(
    'SKIP'
    'SKIP'
    'SKIP'
)
install=zfs.install

prepare() {
    sed -i "s/_KV_=.*/_KV_=${_kernel_version}/" ${startdir}/zfs.install
}

build() {
    cd "${srcdir}/spl"
    ./autogen.sh
    unset LDFLAGS # configure chokes on this otherwise.
    ./configure --prefix=/usr \
                --libdir=/usr/lib \
                --sbindir=/usr/bin \
                --with-linux=/usr/lib/modules/${_kernel_version}-ARCH/build \
                --with-linux-obj=/usr/lib/modules/${_kernel_version}-ARCH/build
    make ${MAKEFLAGS}

    cd "${srcdir}/zfs"
    ./autogen.sh
    #unset LDFLAGS
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --sbindir=/usr/bin \
                --with-mounthelperdir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/lib/udev \
                --libexecdir=/usr/lib/zfs \
                --with-spl=${srcdir}/spl \
                --with-linux=/usr/lib/modules/${_kernel_version}-ARCH/build \
                --with-linux-obj=/usr/lib/modules/${_kernel_version}-ARCH/build
    make ${MAKEFLAGS}
}

package() {
    cd "${srcdir}/spl"
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install

    install -D -t ${pkgdir}/etc/modprobe.d ../zfs.conf

    # Move /lib to /usr/lib.
    cp -r "$pkgdir"/{lib,usr}
    rm -r "$pkgdir"/lib

    # Fix permissions.
    chmod 750 "${pkgdir}/etc/sudoers.d"

    # Remove unneeded files.
    rm -r "${pkgdir}/usr/src"
    rm -r "${pkgdir}/usr/lib/dracut"
    rm -r "${pkgdir}/usr/share/zfs/zfs-tests"
}
